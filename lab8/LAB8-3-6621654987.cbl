      ******************************************************************
      * Author:kasidit
      * Date:22/09/2025
      * Purpose:
      * Tectonics: cobc
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. SORT-WITH-OUTPUT-PROC.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
           SELECT SALES-IN-FILE ASSIGN TO "sales_unsorted.txt"
               ORGANIZATION IS LINE SEQUENTIAL.
           SELECT REPORT-OUT-FILE ASSIGN TO "sales_report.txt"
               ORGANIZATION IS LINE SEQUENTIAL.
           SELECT SORT-WORK-FILE ASSIGN TO "sortwork.tmp".

       DATA DIVISION.
       FILE SECTION.
       FD SALES-IN-FILE.
       01 SALES-IN-REC PIC X(35).

       FD REPORT-OUT-FILE.
       01 REPORT-OUT-REC PIC X(51).

       SD SORT-WORK-FILE.
       01 SORT-REC.
           05 SORT-REGION PIC X(10).
           05 SORT-SALESPERSON PIC X(10).
           05 SORT-AMOUNT PIC 9(5)V99.

       WORKING-STORAGE SECTION.
       01 WS-FLAGS.
           05 WS-SORT-EOF-FLAG PIC X VALUE 'N'.
           88 WS-END-OF-SORT-FILE VALUE 'Y'.

       01 WS-CONTROL-BREAK-FIELDS.
           05 WS-PREVIOUS-REGION PIC X(10) VALUE SPACES.
           05 WS-REGION-SUBTOTAL PIC 9(8)V99 VALUE ZERO.
           05 WS-GRAND-TOTAL PIC 9(9)V99 VALUE ZERO.


       01 WS-HEADER-1 PIC X(51) VALUE "*****************************".
       01 WS-HEADER-2 PIC X(51) VALUE "REGIONAL SALES SUMMARY REPORT".

       01 WS-COLUMN-HEADER PIC X(51)
       VALUE "Region     Salesperson  Amount".

       01 WS-COLUMN-UNDERLINE PIC X(51)
       VALUE "------     -----------  ------".

       01 WS-DETAIL-LINE.
           05 DET-REGION PIC X(10).
           05 DET-SALESPERSON PIC X(10).
           05 DET-AMOUNT PIC ZZ,ZZZ.ZZ.

       01 WS-SUBTOTAL-LINE.
       05 FILLER PIC X(20) VALUE "**REGION SUBTOTAL".
       05 DISP-SUBTOTAL PIC ZZ,ZZZ.ZZ.

       01 WS-GRAND-TOTAL-LINE.
       05 FILLER PIC X(15) VALUE "GRAND TOTAL:".
       05 DISP-GRAND-TOTAL PIC ZZ,ZZZ.99.


       PROCEDURE DIVISION.

       1000-MAIN-PROCEDURE.
           SORT SORT-WORK-FILE
               ON ASCENDING KEY SORT-REGION
               USING SALES-IN-FILE
               OUTPUT PROCEDURE IS 3000-CREATE-SALES-REPORT.
           STOP RUN.

       3000-CREATE-SALES-REPORT SECTION.

       3001-START-PROCEDURE.
           OPEN OUTPUT REPORT-OUT-FILE.

           WRITE REPORT-OUT-REC FROM WS-HEADER-1.
           WRITE REPORT-OUT-REC FROM WS-HEADER-2.
           WRITE REPORT-OUT-REC FROM WS-HEADER-1.
           *>WRITE REPORT-OUT-REC FROM SPACES.

           WRITE REPORT-OUT-REC FROM WS-COLUMN-HEADER.
           WRITE REPORT-OUT-REC FROM WS-COLUMN-UNDERLINE.
           *>WRITE REPORT-OUT-REC FROM SPACES.

           PERFORM 3200-RETURN-NEXT-SORTED-RECORD.
           PERFORM 3300-PROCESS-SORTED-RECORDS
               UNTIL WS-END-OF-SORT-FILE.

           PERFORM 3310-PRINT-REGION-SUBTOTAL.

           *>WRITE REPORT-OUT-REC FROM SPACES.
           MOVE ALL "=" TO REPORT-OUT-REC.
           WRITE REPORT-OUT-REC.

           MOVE WS-GRAND-TOTAL TO DISP-GRAND-TOTAL.
           WRITE REPORT-OUT-REC FROM WS-GRAND-TOTAL-LINE.
           WRITE REPORT-OUT-REC FROM WS-HEADER-1.

           CLOSE REPORT-OUT-FILE.

       3002-EXIT-PROCEDURE.
           EXIT.

       3200-RETURN-NEXT-SORTED-RECORD.
           RETURN SORT-WORK-FILE RECORD
               AT END
                   SET WS-END-OF-SORT-FILE TO TRUE
               END-RETURN.

       3300-PROCESS-SORTED-RECORDS.
           IF SORT-REGION NOT = WS-PREVIOUS-REGION
               PERFORM 3310-PRINT-REGION-SUBTOTAL
           END-IF.
           PERFORM 3320-PROCESS-DETAIL-LINE.
           PERFORM 3200-RETURN-NEXT-SORTED-RECORD.

       3310-PRINT-REGION-SUBTOTAL.
           IF WS-PREVIOUS-REGION NOT = SPACES
               *>WRITE REPORT-OUT-REC FROM SPACES
               MOVE WS-REGION-SUBTOTAL TO DISP-SUBTOTAL
               WRITE REPORT-OUT-REC FROM WS-SUBTOTAL-LINE
               *>WRITE REPORT-OUT-REC FROM SPACES
               ADD WS-REGION-SUBTOTAL TO WS-GRAND-TOTAL
           END-IF.
           MOVE ZERO TO WS-REGION-SUBTOTAL.
           MOVE SORT-REGION TO WS-PREVIOUS-REGION.

       3320-PROCESS-DETAIL-LINE.
           ADD SORT-AMOUNT TO WS-REGION-SUBTOTAL.
           INITIALIZE WS-DETAIL-LINE.
           MOVE SORT-REGION TO DET-REGION.
           MOVE SORT-SALESPERSON TO DET-SALESPERSON.
           MOVE SORT-AMOUNT TO DET-AMOUNT.
           WRITE REPORT-OUT-REC FROM WS-DETAIL-LINE.
